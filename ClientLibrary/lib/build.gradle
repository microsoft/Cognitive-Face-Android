apply plugin: 'com.android.library'

android {
    compileSdkVersion 22

    defaultConfig {
        minSdkVersion 22
        targetSdkVersion 22
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.squareup.okhttp3:okhttp:3.13.1'
}

apply plugin: 'maven'
apply plugin: 'signing'

signing {
    sign configurations.archives
}

// Group ID is the project name
group = "com.microsoft.projectoxford"
// Artifact name is the name of the technology
archivesBaseName = "face"
// Update your version
version = "1.4.3"

// Upload artifacts to maven central repository staging servers
uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                // The readable name of the artifact
                name 'Microsoft Project Oxford Face Client Library'
                packaging 'jar'

                // optionally artifactId can be defined here

                // Descriptions of the artifacts.
                description 'This client library allows the use of Microsoft\'s state-of-the-art cloud-based face algorithms to detect and recognize human faces in images. See https://github.com/Microsoft/Cognitive-Face-Android for more information.'

                // Project URL
                url 'https://github.com/Microsoft/Cognitive-Face-Android'

                // Github information
                scm {
                    connection 'scm:git:https://github.com/Microsoft/Cognitive-Face-Android'
                    developerConnection 'scm:git:https://github.com/Microsoft/Cognitive-Face-Android'
                    url 'scm:git:https://github.com/Microsoft/Cognitive-Face-Android'
                }

                licenses {
                    license {
                        name 'MIT'
                        url 'https://github.com/Microsoft/Cognitive-Face-Android/blob/master/LICENSE.md'
                    }
                }

                developers {
                    developer {
                        id 'projectoxfordSDK'
                        name 'Project Oxford Client SDK'
                        email 'projectoxfordsdk@microsoft.com'
                    }
                }
            }
        }
    }
}